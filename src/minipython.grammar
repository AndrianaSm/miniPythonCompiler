/*
* Test Comments
*/

Package minipython;

Helpers
	digit = ['0' .. '9'];
	letter = ['a' .. 'z']|['A' .. 'Z'];
	cr = 13;
	lf = 10;
	space = 32;
	single_quotes = ''';
    double_quotes = '"';
    underscore = '_';
	all = [0..127];
	eol = lf | cr | cr lf ;
	not_eol = [all - [cr + lf]];

Tokens
	tab = 9;
    def='def';
    l_par = '(';
    r_par = ')';
	eq = '=';
	comma=',';
	semi = ':';
	if = 'if';
	while = 'while';
    for = 'for';
    in = 'in';
    print = 'print';
    return = 'return';
    meq = '-=';
    seq = '/=';
    l_br = '[';
    r_br = ']';
    assert = 'assert';
    plus = '+';
    minus = '-';
    mult = '*';
    div = '/';
    mod = '%';
	dmult = '**';
	none = 'None';
    open = 'open';
    type = 'type';
    max = 'max';
    min = 'min';
    and = 'and';
    or = 'or';
    not = 'not';
    less = '<';
    great = '>';
    lesseq = '<=';
    greateq = '>=';
    noteq = '!=';
	eqeq = '==';
    true = 'true';
    false = 'false';
    dot = '.';
	blank = (space | lf | cr);
	id = (underscore)*letter (letter | digit |underscore)*;
	line_comment = '#' not_eol* eol;
	string = double_quotes not_eol* double_quotes | single_quotes not_eol* single_quotes;
	num = digit+ | (digit+ '.' digit+);

Ignored Tokens
	blank, line_comment;

Productions
	goal = commands*{->New goal([commands])};

	commands =	{stat} statement {->New commands.stat(statement)}|
				{func} function{->New commands.func (function)};

	function = def id l_par argument? r_par semi statement{-> New function(id,[argument],statement)};

	argument = arg nextarg*{-> New argument(arg,[nextarg])};

	arg = id defaultval?{->New arg(id,[defaultval])} ;

	defaultval = eq value{->New defaultval(value)};

	nextarg = comma id defaultval? {-> New nextarg(id,[defaultval])};

	statement =	{if}		tab* if boolexpr semi statement {-> New statement.if(boolexpr, statement)}|
				{while}		tab* while boolexpr semi statement {-> New statement.while(boolexpr, statement)}|
				{for}		tab* for [element]:id in [collection]:id semi statement {->New statement.for(element,collection,statement)}|
				{return}	tab* return expression {->New statement.return(expression)}|
				{print}		tab* print expression commaexpression* {->New statement.print(expression,[commaexpression])}|
				{equals}	tab* id equalsigns expression {->New statement.equals(id,equalsigns,expression)}|
				{array}		tab* id l_br [index]:expression r_br eq [valarray]:expression {->New statement.array(id,index,valarray)}|
				{assert}	tab* assert expression commaexpression? {->New statement.assert(expression,[commaexpression])}|
				{funccall}	tab* functioncall{->New statement.funccall(functioncall)};


	commaexpression = comma expression{->New commaexpression(expression)};

	equalsigns = {eq} eq {->New equalsigns.eq()}|
				{meq} meq {->New equalsigns.meq()}|
				{seq} seq {->New equalsigns.seq()};

	expression =
    					{binary}	arithmetics {->New expression.binary(arithmetics)}|
    					{open}		open l_par [first]:expression comma [second]:expression r_par {->New expression.open(first,second)}|
    					{type}		type l_par id r_par {->New expression.type(id)}|
    					{brackets}	l_br expression commaexpression* r_br{->New expression.brackets(expression,[commaexpression])};

    	nextvalue = comma value{->New nextvalue(value)};

        arithmetics = {mult} multiplication {->New arithmetics.mult(multiplication)}|
    	              {plus} [left]:arithmetics plus [right]:multiplication {->New arithmetics.plus(left,right)}|
    	              {minus} [left]:arithmetics minus [right]:multiplication {->New arithmetics.minus(left,right)};

        multiplication = {element} powerorelement {->New multiplication.element(powerorelement)}|
    	                 {mult} [left]:multiplication mult [right]:powerorelement {->New multiplication.mult(left,right)}|
    	                 {div} [left]:multiplication div [right]:powerorelement {->New multiplication.div(left,right)}|
    	                 {mod} [left]:multiplication mod [right]:powerorelement {->New multiplication.mod(left,right)};

        powerorelement = {element} element {-> element.powerorelement}|
                          {power} power{-> power.powerorelement};

        power{-> powerorelement} = [elem]:element dmult [exponent]:element {-> New powerorelement.power(elem.powerorelement,exponent.powerorelement)};

    	element{-> powerorelement} = {max}  max l_par value nextvalue+ r_par {->New powerorelement.max(value,[nextvalue])}|
                                     {min}	min l_par value nextvalue+ r_par {->New powerorelement.min(value,[nextvalue])}|
                                     {array}    id l_br expression r_br {->New powerorelement.array(id,expression)}|
                                     {value} value {->New powerorelement.value(value)}|
    	                             {id} id {->New powerorelement.id(id)}|
    			                     {funccall}	functioncall {->New powerorelement.funccall(functioncall)}|
    			                     {pars}     l_par expression r_par {->New powerorelement.pars(expression)};


    boolexpr = {rule1} boolexprpart andor boolexpr {-> New boolexpr.rule1(boolexprpart.boolexpr,andor,boolexpr)}
                |{rule2} not boolexprpart {-> New boolexpr.rule2(boolexprpart.boolexpr)}
                |{rule3} boolexprpart{->  boolexprpart.boolexpr};

    boolexprpart{-> boolexpr} = {value} boolvalue {-> boolvalue.boolexpr}
                        | {comparison} comparison {-> comparison.boolexpr}
                        | {pars} l_par boolexpr r_par {-> boolexpr};

    boolvalue{-> boolexpr} = {true} true{->New boolexpr.true(true)} |
                {false} false{-> New boolexpr.false(false)};

    comparison{-> boolexpr}= [exp1]:expression comparison_operator [exp2]:expression{->New boolexpr.comparison(exp1,comparison_operator,exp2)};


    andor = {and} and {->New andor.and()}|
     			{or}  or {->New andor.or()};

	comparison_operator = {great} great {->New comparison_operator.great()}|
			                      {less} less {->New comparison_operator.less()}|
			                      {greateq} greateq {->New comparison_operator.greateq()}|
			                      {lesseq} lesseq {->New comparison_operator.lesseq()}|
			                      {eqeq} eqeq {->New comparison_operator.eqeq()}|
			                      {noteq} noteq {->New comparison_operator.noteq()};

	functioncall = id l_par arglist? r_par {->New functioncall(id,[arglist])};

	arglist = expression commaexpression* {->New arglist(expression,[commaexpression])};

	value = {funccall}	id dot functioncall {->New value.funccall(id,functioncall)}|
			{num}		num {->New value.num(num)}|
			{str_l}		string {->New value.str_l(string)}|
			{none}		none {->New value.none(none)};


Abstract Syntax Tree

	goal = commands*;

	commands =	{stat} statement |
				{func} function;

	function = id argument* statement;

	argument = arg nextarg*;

	arg = id defaultval* ;

	defaultval = value;

	nextarg = id defaultval* ;

	 statement =    {if}        boolexpr statement |
                    {while}	    boolexpr statement |
                	{for}		[element]:id [collection]:id statement |
                    {return}	expression |
                	{print}     expression commaexpression* |
				    {equals}	id equalsigns expression |
				    {array}		id  [index]:expression  [valarray]: expression |
				    {assert}	expression commaexpression* |
				    {funccall}	functioncall;

    commaexpression = expression;

    equalsigns =    {eq} |
    			    {meq} |
    				{seq};

    expression =
        					{binary}	arithmetics |
        					{open}		 [first]:expression  [second]:expression  |
        					{type}		  id  |
        					{brackets}	 expression commaexpression* ;

    nextvalue = value;

    arithmetics = {mult} multiplication |
        	              {plus} [left]:arithmetics  [right]:multiplication |
        	              {minus} [left]:arithmetics  [right]:multiplication;

    multiplication = {element} powerorelement |
                     {mult} [left]:multiplication  [right]:powerorelement |
        	         {div} [left]:multiplication  [right]:powerorelement |
        	         {mod} [left]:multiplication  [right]:powerorelement ;

    powerorelement ={max} value nextvalue+ |
				 {min} value nextvalue+ |
				 {array} id expression |
				 {value} value |
				 {id} id |
				 {funccall} functioncall |
				 {pars} expression |
				 {power} [elem]:powerorelement [exponent]:powerorelement ;

    boolexpr = {rule1} [bool1]:boolexpr andor  [bool2]:boolexpr |
               {rule2} boolexpr |
                {comparison} [exp1]:expression comparison_operator [exp2]:expression |
                {pars}  boolexpr |
                {true} true|
                {false} false ;

    andor = {and} |
         	{or};

    comparison_operator =   {great} |
    			            {less} |
    			            {greateq} |
    			            {lesseq} |
    			            {eqeq} |
    			            {noteq};

    functioncall = id arglist*;

    arglist = expression commaexpression*;

    value = {funccall} id functioncall |
    		{num} num |
    		{str_l}	string |
    		{none} none;


